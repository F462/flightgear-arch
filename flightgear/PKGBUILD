# Maintainer: acxz <akashpatel2008 at yahoo dot com>
# Contributor: Ashley Whetter <(firstname) @ awhetter.co.uk>
# Contributor: Deon Spengler <deon at spengler dot co dot za>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Hans Janssen <hans@janserv.xs4all.nl>

pkgname='flightgear'
pkgver=2024.1.1
pkgrel=1
pkgdesc="An open-source, multi-platform flight simulator"
arch=('x86_64')
depends=('libxmu' 'libxi' 'zlib' 'openscenegraph' 'libxrandr' 'glu' 'openal')
makedepends=('boost' 'cmake' 'mesa' 'sharutils' "simgear>=${pkgver}" 'qt5-base' 'qt5-declarative' 'qt5-svg')
optdepends=('qt5-base: fgfs --launcher'
            'qt5-declarative: fgfs --launcher'
            'flightgear-data')
license=("GPL")
url="http://www.flightgear.org/"
source=("https://gitlab.com/flightgear/fgmeta/-/jobs/9264813015/artifacts/raw/fgbuild//${pkgname}-${pkgver}.tar.bz2"
        "fix-openal.patch")
sha256sums=('3a17903cebd56043c55305ff78e793ed80bdcc89d8e8a141d27e62563e313859'
            '82fea83f435c43a921ecde8791a4761a36a52da22f2f8a99c44ad5b7d98f6893')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch --forward --strip=1 --input="${srcdir}/fix-openal.patch"
  sed -i 's|Exec=.*|Exec=fgfs --fg-root=/usr/share/flightgear/data|' package/org.flightgear.FlightGear.desktop.in
}

build() {
  mkdir -p "$srcdir"/fgbuild
  cd "$srcdir"/fgbuild
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DFG_DATA_DIR:STRING="/usr/share/flightgear/data" \
    -DCMAKE_BUILD_TYPE=Release \
    ../$pkgname-$pkgver
  make
}

package() {
  cd "$srcdir"/fgbuild
  make DESTDIR=${pkgdir} install

  cd "$srcdir"/flightgear-$pkgver
  install -Dm0644 package/flightgear.ico "$pkgdir"/usr/share/icons/flightgear.ico
  install -Dm0644 scripts/completion/fg-completion.bash "$pkgdir"/usr/share/bash-completion/completions/fgfs
  install -Dm0644 scripts/completion/fg-completion.zsh "$pkgdir"/usr/share/zsh/site-functions/_fgfs
  ln -sf flightgear "$pkgdir"/usr/share/FlightGear
}
